apply plugin: 'java'
apply plugin: 'idea'

repositories {
    flatDir { dirs "$projectDir/libs" }
}

evaluationDependsOn(':webapp')

configurations {
    deb
}

dependencies {
    compile 'org.jruby:jruby-complete:1.7.0.preview1'
    compile 'org.rubygems:jruby-openssl:0.7.7'

    compile name: 'arr-pm', version: '0.0.7-requirefix'

    compile('org.rubygems:fpm:0.4.10') {
        exclude module: 'arr-pm'
    }
}

processResources.dependsOn << ':webapp:war'

processResources << {
    copy {
        from sourceSets.main.resources
        into 'build/tmp/main'
    }

    copy {
        from project(':webapp').war.archivePath
        into 'build/tmp/main/var/lib/gemjar'
    }
}

processResources << {
    copy {
        from sourceSets.nginxConfiguration.resources
        into 'build/tmp/nginxConfiguration'
    }

    file('build/tmp/nginxConfiguration/var/www/cache').mkdirs()
}

sourceSets {
    nginxConfiguration
}

task(resources) << {
    file("$buildDir/tmpdir").mkdirs()
    file("$buildDir/libs").mkdirs()
}

artifacts {
    deb file("$buildDir/libs/gemjar.deb")
    deb file("$buildDir/libs/nginx-configuration.deb")
}

task('application', type: JavaExec, dependsOn: ['resources', 'processResources']) {
    inputs.dir sourceSets.main.resources
    inputs.file project(':webapp').war.archivePath
    outputs.file "$buildDir/libs/gemjar.deb"

    main 'org.jruby.Main'
    classpath runtimeClasspath
    args "-r", "$projectDir/src/main/ruby/patches/fpm.rb"
    args "-S fpm -p $buildDir/libs/gemjar.deb -s dir -t deb -n gemjar -a all -d openjdk-6-jdk -C $buildDir/tmp/main .".split(' ')
}

task('nginxConfiguration', type: JavaExec, dependsOn: ['resources', 'processResources']) {
    inputs.dir sourceSets.main.resources
    outputs.file "$buildDir/libs/nginx-configuration.deb"

    main 'org.jruby.Main'
    classpath runtimeClasspath
    args "-r", "$projectDir/src/main/ruby/patches/fpm.rb"
    args "-S fpm -p $buildDir/libs/nginx-configuration.deb -s dir -t deb -n nginx-configuration -a all -d nginx -C $buildDir/tmp/nginxConfiguration .".split(' ')
}

build.dependsOn << 'application'
build.dependsOn << 'nginxConfiguration'
