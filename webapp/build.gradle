apply plugin: 'war'

configurations {
    bootstrap
    bootstrap.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.servlet'

    compile.extendsFrom bootstrap
}

dependencies {
    bootstrap 'org.eclipse.jetty:jetty-server:8.1.4.v20120524'
    bootstrap 'org.eclipse.jetty:jetty-servlet:8.1.4.v20120524'
    bootstrap 'org.eclipse.jetty:jetty-io:8.1.4.v20120524'
    bootstrap 'org.eclipse.jetty:jetty-util:8.1.4.v20120524'
    bootstrap 'org.eclipse.jetty:jetty-webapp:8.1.4.v20120524'
    bootstrap 'javax.servlet:javax.servlet-api:3.0.1'

    compile 'org.jruby:jruby-core:1.6.7.2'
    compile 'org.jruby:jruby-stdlib:1.6.7.2'
    compile 'com.jcraft:jzlib:1.1.1'

    compile('org.jruby.rack:jruby-rack:1.1.7') {
        transitive = false
    }

    compile 'org.rubygems:sinatra:1.3.2'
    compile 'org.rubygems:builder:3.0.0'
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/ruby'
        }
    }
}

war {
    from configurations.bootstrap.collect { it.isDirectory() ? it : zipTree(it) } + files(sourceSets.main.output.classesDir)
    classpath = runtimeClasspath - configurations.bootstrap
    manifest {
        attributes 'Main-Class': 'gemjar.WebServer'
    }
}

artifacts {
    war
}

task(run, type: JavaExec, dependsOn: 'war') {
    main 'gemjar.WebServer'
    classpath war.archivePath
}
